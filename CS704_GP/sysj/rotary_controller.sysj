// controller responsible for managing the rotary model
// author: Nikolay Petin

// named reaction for converting the ready channel signals to regular signals
reaction ch_to_sig(: input Boolean channel ch_in, output signal sig_out)	// sysimport doesn't let compilation
{
	{
		while(true) {
			receive ch_in;
			pause;
		}
	}
	||
	{
		while(true) {
			if(#ch_in == Boolean.TRUE) {
				emit sig_out;
			}
			pause;
		} 
	}
}

rotary_controller(
		// from model
		input signal tableAlignedWithSensor;
		input signal bottleAtPos1, bottleAtPos5;
		input signal rotaryEmpty;
		
		// from conveyor
		input signal conveyorEngaged;

		// from buffer
		input Integer channel newOrder;
		
		// from component controllers
		input Boolean channel readyConveyor, readyFiller, readyLidLoader, readyCapper;
		
		// to model
		output signal rotaryTableTrigger; 
		output signal load, deload;

		)
->
{
	signal rC, rF, rL, rCa;	// ready signals
	{
		while(true) {
			await (rC && rF && rL && rCa);
			await (!rotaryEmpty && !bottleAtPos5);			// only rotate when there is at least one bottle on the table except for pos 5
			abort (immediate tableAlignedWithSensor) { 				
				sustain rotaryTableTrigger;					// rotate table until aligned as indicated by tableAlignedWithSensor
			}
			pause;
			pause;	// double pause to stop rotary table from rotating mid-filling operation
		}
	}
	||
	{ // loading logic
		while(true) {
			receive newOrder;
			abort(immediate bottleAtPos1) {
				sustain load;						// sustain load until bottle is confirmed at pos 1, this approach results in the rotary table loading multiple times at once, which could pose a problem for a pipelined solution
			}										
			pause;
		}
	}
	||
	{ // deloading logic
		while(true) {
			await(bottleAtPos5 && conveyorEngaged);
			abort(immediate !bottleAtPos5){			
				sustain deload;						// sustain deload until bottle is confirmed to have left
			}
			pause;
		}
	}
	||
	{
		{
			ch_to_sig(:readyConveyor, rC);
		}
		||
		{
			ch_to_sig(:readyFiller, rF);
		}
		||
		{
			ch_to_sig(:readyLidLoader, rL);
		}
		||
		{
			ch_to_sig(:readyCapper, rCa);
		}
	}
}