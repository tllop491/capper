// Unit for managing the rotary plant

import rotaryTable.*;

rotary_plant(
		input signal rotaryTableTrigger; // turns rotary table while signal is TRUE
		input signal load, deload;
		output signal tableAlignedWithSensor;
		output signal bottleAtPos1, bottleAtPos1c, bottleAtPos2, bottleAtPos3, bottleAtPos4, bottleAtPos5, bottleLeftPos5;
		
		output signal tableAlignedWithSensor_GUI;
		output signal bottleAtPos1_GUI, bottleAtPos2_GUI, bottleAtPos3_GUI, bottleAtPos4_GUI, bottleAtPos5_GUI, bottleLeftPos5_GUI;
		output signal empty;
		)
->
{	

	signal shift;
	{	// logic for shifting
		while(true) {
			await(rotaryTableTrigger);						// wait for turning signal
			abort(immediate !rotaryTableTrigger) {			// while its on
				waitl(500ms);								// wait 500ms to simulate rotation
				{sustain tableAlignedWithSensor;}			
				||
				{sustain tableAlignedWithSensor_GUI;}		// send confirmation that turn is complete
			}
			emit shift;
			pause;
	}
	}
	|| // logic for loading/deloading and displaying bottleAtPos
	{
		rotaryTable rt = new rotaryTable();
		while(true) {
			present(shift){
				rt.print();									//rotational table printing index array
				System.out.println("SHIFTING");
				rt.shift();									//rotational table printing index shift
				rt.print();
			}
			present(load){
				rt.print();									
				System.out.println("LOADING");
				rt.load();									//rotational table load at first index
				rt.print();
			}
			present(deload){
				rt.print();
				System.out.println("DELOADING");
				rt.deload();								//rotational table remove last index 
				rt.print();
				emit bottleLeftPos5;
				emit bottleLeftPos5_GUI;
			}
			
			// Photoeye sensor logic for emitting bottleAtPosX signals
			
			if (rt.index[0] == true) {
				emit bottleAtPos1;
				emit bottleAtPos1c;
				emit bottleAtPos1_GUI;
			}
			
			if (rt.index[1] == true) {
				emit bottleAtPos2;
				emit bottleAtPos2_GUI;
			}
			
			if (rt.index[2] == true) {
				emit bottleAtPos3;
				emit bottleAtPos3_GUI;
			}
			
			if (rt.index[3] == true) {
				emit bottleAtPos4;
				emit bottleAtPos4_GUI;
			}
			
			if (rt.index[4] == true) {
				emit bottleAtPos5;
				emit bottleAtPos5_GUI;
			}
			pause;
		}
	}
	||
	{
		while(true) {
			present(!bottleAtPos1 && !bottleAtPos1c && !bottleAtPos2 && !bottleAtPos3 && !bottleAtPos4 && !bottleAtPos5) {
				emit empty;
			}
			pause;
		}
	}
}